PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
#Library and Information Science journals
#added before 2016-10
SELECT DISTINCT ?journal ?name WHERE {
    ?journal wdt:P31 wd:Q5633421 . # is scientific journal
    {
            { ?journal wdt:P921 wd:Q199655 }   # with topic library science
      UNION { ?journal wdt:P921 wd:Q16387 }    # and/or topic information science
      UNION { ?journal wdt:P921 wd:Q13420675 } # and/or topic library and information science  
    }
    OPTIONAL{?journal rdfs:label ?en . FILTER(LANG(?en) = "en")}
	BIND(str(COALESCE(?en,strafter(str(?journal),"http://www.wikidata.org/entity/"))) AS ?name)
}
ORDER BY ?journal ?name
