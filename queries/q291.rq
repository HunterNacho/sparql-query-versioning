PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
#Musicians born in Rotterdam (the Netherlands)
select distinct ?item ?itemLabel ?itemDescription where {
    ?item wdt:P106/wdt:P279* wd:Q639669 .
    ?item wdt:P19 wd:Q34370 .
    OPTIONAL{?item rdfs:label ?en . FILTER(LANG(?en) = "en")}
    OPTIONAL{?item rdfs:label ?nl . FILTER(LANG(?nl) = "nl")}
	BIND(str(COALESCE(?en,?nl,strafter(str(?item),"http://www.wikidata.org/entity/"))) AS ?itemLabel)
    OPTIONAL{?item schema:description ?den . FILTER(LANG(?den) = "en")}
    OPTIONAL{?item schema:description ?dnl . FILTER(LANG(?dnl) = "nl")}
	BIND(str(COALESCE(?den,?dnl,strafter(str(?item),"http://www.wikidata.org/entity/"))) AS ?itemDescription)
}
ORDER BY ?item ?itemLabel ?itemDescription
