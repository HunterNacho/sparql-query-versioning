PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
#Biological pathways with protein structures in the PDB database
SELECT ?pathway ?pathwayLabel ?WikiPathways ?Reactome (COUNT(DISTINCT ?protein) as ?count) WHERE {
  VALUES ?pathwayType { wd:Q4915012 wd:Q2996394 }
  ?pathway wdt:P31 ?pathwayType .
  { ?pathway wdt:P527/wdt:P688 ?protein . } UNION { ?pathway wdt:P527 ?protein . }
  ?protein wdt:P638 ?PDBID .
  OPTIONAL { ?pathway wdt:P2410 ?WikiPathways }
  OPTIONAL { ?pathway wdt:P3937 ?Reactome }
  OPTIONAL{?pathway rdfs:label ?en . FILTER(LANG(?en) = "en")}
	BIND(str(COALESCE(?en,strafter(str(?pathway),"http://www.wikidata.org/entity/"))) AS ?pathwayLabel)
} GROUP BY ?pathway ?pathwayLabel ?WikiPathways ?Reactome
  ORDER BY DESC(?count) ?pathway ?pathwayLabel ?WikiPathways ?Reactome
