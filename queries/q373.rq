PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
#Most frequent occupations without a label in a given language
#added before 2016-10
#Most frequent occupations without a label in Latvian (lv)
#by Edgars2007/Jura1, 2016-05-09
SELECT ?occup ?occupLabel ?count
WHERE
{
	{
		SELECT ?occup (COUNT(?person) as ?count)
		WHERE
		{
			?person wdt:P106 ?occup
		}
		GROUP BY ?occup
		ORDER BY DESC(?count)
		LIMIT 1000
	}
  	OPTIONAL {?occup rdfs:label ?label1 	filter(lang(?label1) = "lv")}
	FILTER(!BOUND(?label1))
 
        OPTIONAL{?occup rdfs:label ?en . FILTER(LANG(?en) = "en")}
        OPTIONAL{?occup rdfs:label ?es . FILTER(LANG(?es) = "es")}
	OPTIONAL{?occup rdfs:label ?pl . FILTER(LANG(?pl) = "pl")}
        OPTIONAL{?occup rdfs:label ?ru . FILTER(LANG(?ru) = "ru")}
	OPTIONAL{?occup rdfs:label ?ja . FILTER(LANG(?ja) = "ja")}
	OPTIONAL{?occup rdfs:label ?zh . FILTER(LANG(?zh) = "zh")}
	BIND(str(COALESCE(?en,?es,?pl,?ja,?zh,?ru,strafter(str(?occup),"http://www.wikidata.org/entity/"))) AS ?occupLabel)
}
ORDER BY DESC(?count) ?occup ?occupLabel
LIMIT 50
