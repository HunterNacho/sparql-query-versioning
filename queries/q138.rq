PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
#How many states this US state borders
#added before 2016-10
SELECT ?state ?stateLabel ?borders
WHERE
{
	{
		SELECT ?state (COUNT(?otherState) as ?borders)
		WHERE
		{
		?state wdt:P31 wd:Q35657 .
		?otherState wdt:P47 ?state .
		?otherState wdt:P31 wd:Q35657 .
		}
		GROUP BY ?state
	}
	OPTIONAL{?state rdfs:label ?en . FILTER(LANG(?en) = "en")}
	BIND(str(COALESCE(?en,strafter(str(?state),"http://www.wikidata.org/entity/"))) AS ?stateLabel)
}        
ORDER BY DESC(?borders) ?state ?stateLabel
