PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
#List of suicide attacks
#added before 2016-10
SELECT ?h ?hLabel ?location (CONCAT("injured: ",str(?injured)) as ?injuredl) (concat("dead: ",str(?dead)) as ?deadl) ?date ?image
WHERE
{
        ?h wdt:P31 ?attack. 
    values (?attack) {
      (wd:Q18493502)
      (wd:Q217327)
    }
    optional {    ?h wdt:P1339 ?injured . }
    optional {    ?h wdt:P1120 ?dead. }
    optional {    ?h wdt:P276?/wdt:P625 ?location }
    optional {    ?h wdt:P585 ?date }
    optional {    ?h wdt:P18 ?image }
    OPTIONAL{?h rdfs:label ?en . FILTER(LANG(?en) = "en")}
	BIND(str(COALESCE(?en,strafter(str(?h),"http://www.wikidata.org/entity/"))) AS ?hLabel)
}
ORDER BY ?h ?hLabel ?location ?injuredl ?deadl ?date ?image
