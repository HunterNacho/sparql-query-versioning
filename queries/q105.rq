PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
SELECT
	?country ?countryLabel
	?wikiPersons
	?population
	(ROUND(?wikiPersons/?population*1000000) AS ?wikiPersonsPerM)
WHERE
{
	{ SELECT ?country (count(*) as ?wikiPersons) WHERE {
		{SELECT DISTINCT ?person ?country WHERE {
			?country wdt:P31 wd:Q185441 .
			?person wdt:P31 wd:Q5 .
			?person wdt:P27 ?country .
			FILTER NOT EXISTS{ ?person wdt:P570 ?date }
		} }
	} GROUP BY ?country  }
	?country wdt:P1082 ?population
	OPTIONAL{?country rdfs:label ?en . FILTER(LANG(?en) = "en")}
	BIND(str(COALESCE(?en,strafter(str(?country),"http://www.wikidata.org/entity/"))) AS ?countryLabel)
}
ORDER BY DESC(?wikiPersonsPerM) ?country ?countryLabel ?wikiPersons	?population
