PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
#Who discovered the most planets (with list)
#added before 2016-10
SELECT
	?discoverer ?discovererLabel 
	(COUNT(DISTINCT ?planet) as ?count)
	(GROUP_CONCAT(DISTINCT(?planetLabel),", ") as ?planets)
WHERE
{
	?ppart wdt:P279* wd:Q634 .
	?planet wdt:P31 ?ppart .
	?planet wdt:P61 ?discoverer .
	OPTIONAL{?discoverer rdfs:label ?en . FILTER(LANG(?en) = "en")}
	BIND(str(COALESCE(?en,strafter(str(?discoverer),"http://www.wikidata.org/entity/"))) AS ?discovererLabel)
	OPTIONAL{?planet rdfs:label ?pen . FILTER(LANG(?pen) = "en")}
	BIND(str(COALESCE(?pen,strafter(str(?planet),"http://www.wikidata.org/entity/"))) AS ?planetLabel)
}
GROUP BY ?discoverer ?discovererLabel
ORDER BY DESC(?count) ?discoverer ?discovererLabel ?planets
