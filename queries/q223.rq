PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
#Authors of scientific articles by occupation
#added before 2016-10
#defaultView:BubbleChart
SELECT ?occupationLabel (count(DISTINCT ?author) as ?count)
WHERE
{
        ?object wdt:P31 wd:Q13442814
        ; wdt:P50 ?author .
        ?author wdt:P106 ?occupation .
	OPTIONAL{?occupation rdfs:label ?en . FILTER(LANG(?en) = "en")}
        OPTIONAL{?occupation rdfs:label ?fr . FILTER(LANG(?fr) = "fr")}
	BIND(str(COALESCE(?en,?fr,strafter(str(?occupation),"http://www.wikidata.org/entity/"))) AS ?occupationLabel)
}
GROUP BY ?occupationLabel
ORDER BY DESC(?count) ?occupationLabel
