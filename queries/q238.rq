PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
#Scientists who have worked together but whose Erdos numbers don???t reflect that
#added before 2016-10
# Finds authors who have published scientific articles together,
# but whose Erdos numbers are more than one apart.
# These would appear to violate the definition of the Erdos number.
SELECT
	# Q#s
	?paper
	?author1
	?author2
	# title (either from title statement or label)
	(IF(BOUND(?title), ?title, ?paperLabel) AS ?titles)
	# author labels (should be names) and their Erdos numbers
	?author1Label
	?erdos1
	?author2Label
	?erdos2
	# distance between Erdos numbers
	?distance
WHERE
{
	# paper, instance of or subclass of scientific article; also has two authors
	?paper wdt:P31/wdt:P279* wd:Q13442814;
	         wdt:P50 ?author1, ?author2.
	# if it has a title, we???ll grab that as well, but it???s also okay if there???s no title
	OPTIONAL { ?paper wdt:P1476 ?title. }
	# grab Erdos numbers of the two authors
	?author1 wdt:P2021 ?erdos1.
	?author2 wdt:P2021 ?erdos2.
	# introduce a new variable for the difference of the Erdos numbers
	BIND(?erdos2 - ?erdos1 AS ?distance).
	# select those cases where the distance is > 1
	# (note: by *not* taking the absolute value of the distance, we avoid getting duplicated cases because the author variables might be swapped)
	FILTER(?distance > 1).
	# get *Label variables automagically
	OPTIONAL{?paper rdfs:label ?en . FILTER(LANG(?en) = "en")}
	BIND(str(COALESCE(?en,strafter(str(?paper),"http://www.wikidata.org/entity/"))) AS ?paperLabel)
	OPTIONAL{?author1 rdfs:label ?oen . FILTER(LANG(?oen) = "en")}
	BIND(str(COALESCE(?oen,strafter(str(?author1),"http://www.wikidata.org/entity/"))) AS ?author1Label)
	OPTIONAL{?author2 rdfs:label ?ten . FILTER(LANG(?ten) = "en")}
	BIND(str(COALESCE(?ten,strafter(str(?author2),"http://www.wikidata.org/entity/"))) AS ?author2Label)
}
# sort by distance first (descending), then by first author, then by second author
ORDER BY DESC(?distance) ?author1Label ?author2Label ?paper ?author1 ?author2 ?titles ?erdos1 ?erdos2
