PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
#Food & Drink
#German breweries
#added before 2016-10
#Locations of breweries in Germany
#defaultView:Map
select ?breweryLabel ?breweryDescription ?coord
WHERE
{
	?brewery wdt:P31/wdt:P279* wd:Q131734 ;
		wdt:P17 wd:Q183 ;
		wdt:P625 ?coord .
	OPTIONAL{?brewery rdfs:label ?en . FILTER(LANG(?en) = "en")}
	OPTIONAL{?brewery rdfs:label ?de . FILTER(LANG(?de) = "de")}
	BIND(str(COALESCE(?en,?de,strafter(str(?brewery),"http://www.wikidata.org/entity/"))) AS ?breweryLabel)
	OPTIONAL{?brewery schema:description ?den . FILTER(LANG(?den) = "en")}
	OPTIONAL{?brewery schema:description ?dde . FILTER(LANG(?dde) = "de")}
	BIND(str(COALESCE(?den,?dde,strafter(str(?brewery),"http://www.wikidata.org/entity/"))) AS ?breweryDescription)
}
ORDER BY ?breweryLabel ?breweryDescription ?coord
