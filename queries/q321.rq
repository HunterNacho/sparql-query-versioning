PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
#Sandwich ingredients
#added before 2016-10
SELECT ?sandwich ?ingredient ?sandwichLabel ?ingredientLabel
WHERE
{
  ?sandwich wdt:P31?/wdt:P279* wd:Q28803;
            wdt:P527 ?ingredient.
  MINUS { ?ingredient wdt:P279* wd:Q7802. }
  OPTIONAL{?sandwich rdfs:label ?en . FILTER(LANG(?en) = "en")}
  OPTIONAL{?sandwich rdfs:label ?fr . FILTER(LANG(?fr) = "fr")}
	BIND(str(COALESCE(?en,?fr,strafter(str(?sandwich),"http://www.wikidata.org/entity/"))) AS ?sandwichLabel)
	
  OPTIONAL{?ingredient rdfs:label ?ien . FILTER(LANG(?ien) = "en")}
  OPTIONAL{?ingredient rdfs:label ?ifr . FILTER(LANG(?ifr) = "fr")}
	BIND(str(COALESCE(?ien,?ifr,strafter(str(?ingredient),"http://www.wikidata.org/entity/"))) AS ?ingredientLabel)
}
ORDER BY UCASE(STR(?sandwichLabel)) ?sandwich ?ingredient ?ingredientLabel
