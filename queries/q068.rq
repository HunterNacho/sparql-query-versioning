PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
#Lighthouses in Norway
#added before 2016-10
#defaultView:Map
SELECT DISTINCT ?item ?itemLabel ?coords ?image
WHERE {
	?item wdt:P31 wd:Q39715 ;
          wdt:P17 wd:Q20 ;
          wdt:P625 ?coords
	OPTIONAL { ?item wdt:P18 ?image } 
	OPTIONAL{?item rdfs:label ?nb . FILTER(LANG(?nb) = "nb")}
	OPTIONAL{?item rdfs:label ?nn . FILTER(LANG(?nn) = "nn")}
	OPTIONAL{?item rdfs:label ?en . FILTER(LANG(?en) = "en")}
	OPTIONAL{?item rdfs:label ?fi . FILTER(LANG(?fi) = "fi")}
	BIND(str(COALESCE(?nb,?nn,?en,?fi,strafter(str(?item),"http://www.wikidata.org/entity/"))) AS ?itemLabel)
}
ORDER BY ?itemLabel ?item ?coords ?image
